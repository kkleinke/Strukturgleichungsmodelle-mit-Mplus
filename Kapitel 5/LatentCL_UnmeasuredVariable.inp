TITLE:    Beispiel Unmeasured variable model - Test auf "Spuriousness"
          
DATA:     FILE IS GMF_Panel_020304.dat;
          
VARIABLE: NAMES ARE qcp_ser con11 con12 con13 con21 con22 con23 con31 con32 con33 
          ff11 ff12 ff13 ff21 ff22 ff23 ff31 ff32 ff33;
          
          USEVARIABLES ARE con11 con12 con13 con21 con22 con23  
          con31 con32 con33 ff11 ff12 ff13 ff21 ff22 ff23 ff31 ff32 ff33;
          
          MISSING ARE all (99);

ANALYSIS: ITERATIONS = 5000;

MODEL:    con1 BY con11 
          con21 (1)     
          con31 (2);    
          con2 BY con12 
          con22 (1)       
          con32 (2);
          con3 BY con13 
          con23 (1)        
          con33 (2);

          ff1 BY ff11   
          ff21 (3)         
          ff31 (4);     
          ff2 BY ff12   
          ff22 (3)
          ff32 (4);
          ff3 BY ff13  
          ff23 (3)
          ff33 (4);

          con2 ON con1 (9)  ! Gleichheitsrestriktionen auf korrespondierende 
          ff1  (10);        ! Stabilitäts- und Cross-Lagged Parameter
          con3 ON con2 (9) 
          ff2  (10); 
          ff2 ON ff1   (11)
          con1 (12);
          ff3 ON ff2   (11) 
          con2 (12);
        
          thirdvar BY con1* ! Spezifikation der latenten Drittvariablen 
          con2              ! Die Ladung von con1 wird freigeschätzt
          con3              ! Die Ladungen entsprechend in diesem Fall
          ff1               ! Regressionsparametern (ON-Option)
          ff2               
          ff3 ;             
          thirdvar@1;       ! Varianz der latenten Drittvariablen wird aus
                            ! Identifikationszwecken auf 1 fixiert
                           
          ff1@0;            ! Die Residualvarianz von ff1 wurde auf null fixiert,
                            ! da sie negativ geschätzt wurde
         
          con2 WITH ff2;
    
          con11 WITH con12 con13; 
          con12 WITH con13; 
          con21 WITH con22 con23;  
          con22 WITH con23; 
          con31 WITH con32 con33;
          con32 WITH con33;
          
          ff11 WITH ff12 ff13;
          ff12 WITH ff13;   
          ff21 WITH ff22 ff23;
          ff22 WITH ff23;
          ff31 WITH ff32 ff33;
          ff32 WITH ff33;   
         
          [con11@0 con12@0 con13@0];   
          [con22 con23] (5);         
          [con32 con33] (6);       
          [ff11@0 ff12@0 ff13@0];   
          [ff22 ff23] (7);           
          [ff31 ff33] (8);
      
          [con1 con2 con3];       
          [ff1 ff2 ff3];    

OUTPUT:   STDYX;TECH4;

