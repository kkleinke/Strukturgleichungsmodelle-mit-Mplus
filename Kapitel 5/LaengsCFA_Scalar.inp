TITLE:    Konfirmatorische Faktorenanalyse mit Längsschnittdaten zur
          Überprüfung der Messinvarianz - Skalare Invarianz

DATA:     FILE IS GMF_Panel_020304.dat;
          
VARIABLE: NAMES ARE qcp_ser con11 con12 con13 con21 con22 con23 con31 con32 con33 
          ff11 ff12 ff13 ff21 ff22 ff23 ff31 ff32 ff33;
          
          USEVARIABLES ARE con11 con12 con13 con21 con22 con23  
          con31 con32 con33 ff11 ff12 ff13 ff21 ff22 ff23 ff31 ff32 ff33;
          
          MISSING ARE all (99);

MODEL:    con1 BY con11 
          con21 (1)        
          con31 (2);       
          con2 BY con12 
          con22 (1)       
          con32 (2);
          con3 BY con13 
          con23 (1)        
          con33 (2);

          ff1 BY ff11   
          ff21 (3)         
          ff31 (4);
          ff2 BY ff12  
          ff22 (3)
          ff32 (4);
          ff3 BY ff13  
          ff23 (3)
          ff33 (4);

          con11 WITH con12 con13; 
          con12 WITH con13;       
          con21 WITH con22 con23;
          con22 WITH con23; 
          con31 WITH con32 con33;
          con32 WITH con33;
          
          ff11 WITH ff12 ff13;   
          ff12 WITH ff13;   
          ff21 WITH ff22 ff23;
          ff22 WITH ff23;
          ff31 WITH ff32 ff33;
          ff32 WITH ff33;   
         
          [con11@0 con12@0 con13@0];  
          [con21 con22 con23] (5);     ! Gleichsetzen der Intercepts für     
          [con31 con32 con33] (6);     ! gleiche Indikatoren über die Zeit
          [ff11@0 ff12@0 ff13@0];    
          [ff21 ff22 ff23] (7);
          [ff31 ff32 ff33] (8);
      
          [con1 con2 con3];       
          [ff1 ff2 ff3];    

OUTPUT:   MODINDICES;




